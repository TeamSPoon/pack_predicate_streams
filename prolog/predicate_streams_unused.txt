% File: /opt/PrologMUD/pack/logicmoo_base/prolog/logicmoo/util/predicate_streams.pl

:- module(predicate_streams,
          [ 
            with_input_from_predicate/2,
            with_output_to_predicate/2,
            with_error_to_predicate/2,

            with_input_from_predicate_v2/2,
            with_output_to_predicate_v2/2,
            with_error_to_predicate_v2/2,

            with_predicate_input_stream/3,
            with_predicate_output_stream/3,
            is_predicate_stream/1,

            set_current_input/1,
            set_current_output/1,
            set_current_error/1,

            new_predicate_output_stream/2,
            new_predicate_input_stream/2,

            current_error/1
          ]).

:- meta_predicate
        with_input_from_predicate(:, 0),
        with_output_to_predicate(:, 0),
        with_error_to_predicate(:, 0),

   with_input_from_predicate_v2(:, 0),
   with_output_to_predicate_v2(:, 0),
   with_error_to_predicate_v2(:, 0),

        new_predicate_output_stream(:,-),
        new_predicate_input_stream(:,-),

        whatevah(0),
        with_predicate_output_stream(:, -, 0),
        with_predicate_input_stream(:, -, 0).


 	 	 
:- meta_predicate(redo_cleanup_each(0,0,0)).
redo_cleanup_each(Setup,Goal,Cleanup):-
   \+ \+ '$sig_atomic'(Setup), 
   catch( 
     ((Goal, deterministic(DET)),
       '$sig_atomic'(Cleanup),
         (DET == true -> !
          ; (true;('$sig_atomic'(Setup),fail)))), 
      E, 
      ('$sig_atomic'(Cleanup),throw(E))). 


plz_set_stream(S,P):- whatevah(set_stream(S,P)).


current_input_is_user_input:- stream_property(Stream,alias(user_input)),stream_property(Stream,alias(current_input)).
current_output_is_user_output:- stream_property(Stream,alias(user_output)),stream_property(Stream,alias(current_output)).

set_current_input(In):- 
 set_stream(In,  alias(current_input)),
 no_op(current_input_is_user_input -> set_stream(In,  alias(user_input)) ; true),
 set_input(In).

set_current_output(Out):- 
 set_stream(Out,  alias(current_output)),
 no_op(current_output_is_user_output -> set_stream(Out,  alias(user_output)) ; true),
 set_output(Out).

set_current_error(Err):-
 set_stream(Err, alias(current_error)),
 current_input(In), current_output(Out), 
 set_prolog_IO(In,Out,Err).



current_error(Err):-   
  stream_property(Err,alias(current_error))-> true;  % when we set it
  stream_property(Err,alias(user_error)) -> true;
  stream_property(Err,file_no(2)).



%! whatevah( :Goal) is semidet.
%
% Pronounced like a teenage girl
%
system:whatevah(Goal):- ignore(catch(Goal,error(_,_),fail)).


%! with_output_to_predicate( :Pred1, :Goal) is nondet.
%
%  Redirects output stream to a predicate
%
%  `?- with_output_to_predicate(print_as_html_pre,
%    (writeln("hi there"),writeln("how are you?"))).`
%
% @bug Not decided that a with_output_to_predicate/2 should call close or not?
% (flush gets the job done equally as well as closing)

with_output_to_predicate_v2(Pred1,Goal):-
    current_output(Prev),   
    with_predicate_output_stream(Pred1,Stream,
        redo_cleanup_each(set_current_output(Stream),Goal,set_current_output(Prev))).

with_output_to_predicate(Pred1,Goal):-
    current_output(Prev),   
    setup_call_cleanup(
       new_predicate_output_stream(Pred1,Stream),
       redo_cleanup_each(set_current_output(Stream),Goal,set_current_output(Prev)),
       whatevah(close(Stream))).


%! with_error_to_predicate( :Pred1, :Goal) is nondet.
%
%  Redirects error stream to a predicate
%
% `?- with_error_to_predicate(write,threads).`
%
% @bug Not decided that a with_error_to_predicate/2 should call close or not?
% (flush gets the job done equally as well as closing)

with_error_to_predicate(Pred1,Goal):-
    current_error(Prev),   
    setup_call_cleanup(
       new_predicate_output_stream(Pred1,Stream),
       redo_cleanup_each(set_current_error(Stream),Goal,set_current_error(Prev)),
       whatevah(close(Stream))).

with_error_to_predicate_v2(Pred1,Goal):-
    current_error(Prev),
    with_predicate_output_stream(Pred1,Stream,
        redo_cleanup_each(set_current_error(Stream),Goal,set_current_error(Prev))).


%! with_input_from_predicate( :Pred1, :Goal) is nondet.
%
%  `?- with_input_from_predicate((^(X):-X = 'y\n'), poor_interactive_goal).`
%
%  `?- with_input_from_predicate(=('hi.\n'), read(X)).`
%
       
with_input_from_predicate(Pred1,Goal):-
    current_input(Prev),   
    setup_call_cleanup(
       new_predicate_input_stream(Pred1,Stream),
       redo_cleanup_each(set_current_input(Stream),Goal,set_current_input(Prev)),
       (maybe_restore_input(Stream),  % this is a so we dont hit the tracer in Ctrl-C
        whatevah(close(Stream)))).

with_input_from_predicate_v2(Pred1, Goal):-
    current_input(Prev),
    with_predicate_input_stream(Pred1,Stream,       
        redo_cleanup_each(set_current_input(Stream),Goal,set_current_input(Prev))).


is_predicate_stream(Stream):-
   transient_pred_stream:is_pred_stream(Stream).

:- multifile(transient_pred_stream:is_pred_stream/1).
:- dynamic(transient_pred_stream:is_pred_stream/1).
:- volatile(transient_pred_stream:is_pred_stream/1).
:- export(transient_pred_stream:is_pred_stream/1).



% =====================================================
% All the magic is below here
% =====================================================

:- meta_predicate(no_op(0)).
no_op(_).

:- use_module(library(prolog_stream)).

:- thread_local(tl_pred_streams:stream_write/2).
:- module_transparent(tl_pred_streams:stream_write/2).
:- thread_local(tl_pred_streams:stream_close/1).
:- module_transparent(tl_pred_streams:stream_close/1).

% plz_set_stream(Stream, buffer(false)), % useful?
% plz_set_stream(Stream, buffer_size(0)),   % useful? 
% plz_set_stream(Stream, close_on_exec(false)), % useful?
% plz_set_stream(Stream, close_on_abort(false)), % useful?
with_predicate_output_stream(Pred1,Stream,Goal):- 
  open_prolog_stream(tl_pred_streams, write, Stream, []),  
  % set_stream(Stream, buffer(line)),  <- Segfaultz
   setup_call_cleanup(
   ( asserta((tl_pred_streams:stream_write(Stream,Data):- ignore(whatevah(call(Pred1,Data)))),Ref1),
     asserta((tl_pred_streams:stream_close(Stream):- no_op(ignore(whatevah(call(Pred1,end_of_file))))),Ref2),
     asserta(transient_pred_stream:is_pred_stream(Stream),Ref3)),
    Goal,
    % catch so we will not exception on a closed stream
   (whatevah(flush_output(Stream)),
    whatevah(close(Stream)),
    erase(Ref1),erase(Ref2),erase(Ref3))).
  

:- thread_local(tl_pred_streams:stream_read/2).
:- module_transparent(tl_pred_streams:stream_read/2).

with_predicate_input_stream(Pred1,Stream,Goal):-    
   setup_call_cleanup(
   ( open_prolog_stream(tl_pred_streams, read, Stream, []),
     asserta((tl_pred_streams:stream_read(Stream,Data):- ignore(whatevah(call(Pred1,Data)))),Ref1),
     asserta(transient_pred_stream:is_pred_stream(Stream),Ref2)),
    Goal,
   (whatevah(close(Stream)),
    erase(Ref1),erase(Ref2))).



new_predicate_output_stream(Pred1,Stream):-
  open_prolog_stream(tl_pred_streams, write, Stream, []),
  asserta((tl_pred_streams:stream_write(Stream,Data):- ignore(whatevah(call(Pred1,Data)))),Ref1),
  asserta(transient_pred_stream:is_pred_stream(Stream),Ref2),
  asserta((
    tl_pred_streams:stream_close(Stream):- 
    debug(predicate_streams,'~N% ~q.~n',[(stream_close(Stream):-flusing_output_to(Pred1))]),
       % whatevah(call(Pred1,end_of_file)),
       whatevah(flush_output(Stream)),
       whatevah(erase(Ref1)),
       whatevah(erase(Ref2)),
       retractall(tl_pred_streams:stream_close(Stream)))).


new_predicate_input_stream(Pred1,Stream):-
  open_prolog_stream(tl_pred_streams, read, Stream, []),
  asserta((tl_pred_streams:stream_read(Stream,Data):- ignore(whatevah(call(Pred1,Data)))),Ref1),
  asserta(transient_pred_stream:is_pred_stream(Stream),Ref2),
  asserta((
    tl_pred_streams:stream_close(Stream):-    
    maybe_restore_input(Stream),  % this is a so we dont hit the tracer in Ctrl-C
    debug(predicate_streams,'~N% ~q.~n',[(stream_close(Stream):-call(Pred1,end_of_file))]),
       whatevah(call(Pred1,'.\n')),
       whatevah(call(Pred1,end_of_file)),
       whatevah(erase(Ref1)),
       whatevah(erase(Ref2)),       
       retractall(tl_pred_streams:stream_close(Stream)))).


   

maybe_restore_input(Stream):-
  stream_property(Stream,alias(current_input)),
  \+ stream_property(Stream,alias(user_input)),
  stream_property(Was,alias(user_input)),!,
  set_current_input(Was).
maybe_restore_input(_).   
